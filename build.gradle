plugins {
    id 'java'
    id 'maven-publish'
    id "com.gradleup.shadow" version "8.3.6"
    id "io.freefair.lombok" version "8.11"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'me.lidan'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT")
    compileOnly 'org.jetbrains:annotations:23.0.0'
    implementation('dev.triumphteam:triumph-gui:3.1.13') {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation 'com.github.Revxrsal.Lamp:common:3.3.6'
    implementation 'com.github.Revxrsal.Lamp:brigadier:3.3.6'
    implementation 'com.github.Revxrsal.Lamp:bukkit:3.3.6'
    implementation("com.github.cryptomorin:XSeries:13.5.1")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.19")
    }
}

compileJava { // Preserve parameter names in the bytecode
    options.compilerArgs += ["-parameters"]
}

shadowJar {
    archiveClassifier = null
    def packageName = project.name.uncapitalize()
    relocate 'revxrsal.commands', "me.lidan.${packageName}.lamp"
    relocate "dev.triumphteam.gui", "me.lidan.${packageName}.gui"
    relocate "com.cryptomorin.xseries", "me.lidan.${packageName}.xseries"
}

jar.enabled = false

build.dependsOn(shadowJar)

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
